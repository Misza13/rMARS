assembly_file = {
    SOI ~
    (line ~ NEWLINE)* ~
    line? ~
    EOI
}

line = { comment_line | instruction_line | label_line | empty_line }

comment_line = { comment }

comment = { ";" ~ ('\x20'..'\xFF' | WHITESPACE)* }

instruction_line = { (instruction ~ comment?) | (label ~ instruction_line)  }

instruction = { operation ~ fields | pseudo_operation }

label_line = { label+ ~ comment? }

label = @{ alpha ~ (alpha | ASCII_DIGIT)* }

alpha = { ASCII_ALPHA | "_" }

operation = ${ opcode ~ ("." ~ modifier)? }

opcode = {
    ^"DAT" | ^"MOV" |
    ^"ADD" | ^"SUB" | ^"MUL" | ^"DIV" | ^"MOD" |
    ^"JMP" | ^"JMZ" | ^"JMN" | ^"DJN" | ^"CMP" | ^"SLT" | ^"SPL"
}

pseudo_operation = {
    ^"ORG" ~ field |
    ^"EQU" ~ field |
    ^"END" ~ field?
}

modifier = { ^"AB" | ^"BA" | ^"A" | ^"B" | ^"F" | ^"X" | ^"I" }

fields = { field ~ ("," ~ field)? }

field = { mode? ~ expression }

mode = { "#" | "$" | "@" | "<" | ">" }

expression = {
    term ~ "+" ~ expression |
    term ~ "-" ~ expression |
    term ~ "*" ~ expression |
    term ~ "/" ~ expression |
    term ~ "%" ~ expression |
    term
}

term = { label | number | ("(" ~ expression ~ ")") }

number = @{ ("+" | "-")? ~ ASCII_DIGIT+ }

empty_line = _{ WHITESPACE* }

WHITESPACE = _{ " " | "\t" }